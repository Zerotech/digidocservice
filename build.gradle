plugins {
    id "me.seeber.wsimport" version "1.1.1"
    id "maven-publish"
}

group 'ee.zerotech'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.12'
    compileOnly 'org.bouncycastle:bcprov-jdk15on:1.56'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs file('src/integration-test/java')
        }

        resources.srcDirs file('src/integration-test/resources')
    }
}

task integrationTest(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }

    repositories {
        maven {
            url "https://pubnexus.zerotech.ee/content/repositories/zeropublic/"
            credentials {
                username "${pubnexusUser}"
                password "${pubnexusPassword}"
            }
        }
    }
}

